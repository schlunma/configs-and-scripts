###############################################################################
# Configuration file for ZSH
#
# Copyright (c) 2018 Manuel Schlund <schlunma@gmail.com>
# Licensed under the GNU General Public License v3.0 (or later).
###############################################################################



###############################################################################
# Do not load this file for non-interactive shells
###############################################################################
[[ $- == *i* ]] || return
###############################################################################



###############################################################################
# Lines configured by zsh-newuser-install
###############################################################################
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory autocd beep nomatch notify
unsetopt extendedglob
bindkey -v
###############################################################################



###############################################################################
# Lines configured by compinstall
###############################################################################
zstyle :compinstall filename "$HOME/.zshrc"
autoload -Uz compinit
compinit
###############################################################################



###############################################################################
# Basic settings
###############################################################################
KEYTIMEOUT=1                            # Timeout in ZLE mode
autoload -Uz compinit && compinit       # Autocomplete
zstyle ':completion:*' menu select
###############################################################################



###############################################################################
# Prompt
###############################################################################
# Colors
autoload -Uz colors && colors
PROMPT="%{$fg_bold[white]%}%*%{$reset_color%} \
%{$fg_bold[red]%}%n%{$reset_color%}%{$fg[white]%}@\
%{$reset_color%}%{$fg_bold[cyan]%}%m %{$fg_bold[yellow]%}%~ \
"$'\n'"       > %{$reset_color%}"
zle_highlight=(default:fg=green,bold)

# Git
setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:*' stagedstr 'M'
zstyle ':vcs_info:*' unstagedstr 'M'
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' formats "%{$fg_bold[white]%}[%{$reset_color%}\
%{$fg_bold[green]%}%b%{$reset_color%}%{$fg_bold[white]%}]%{$reset_color%} \
%{$fg_bold[green]%}%c%{$reset_color%}%{$fg_bold[yellow]%}%u%{$reset_color%}"
zstyle ':vcs_info:*' actionformats "%{$fg_bold[white]%}[%{$reset_color%}\
%{$fg_bold[green]%}%b%{$reset_color%}%{$fg_bold[white]%}|%{$reset_color%}\
%{$fg_bold[red]%}%a%{$reset_color%}%{$fg_bold[white]%}]%{$reset_color%}"
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
zstyle ':vcs_info:*' enable git
+vi-git-untracked() {
    if [[ $("git" rev-parse --is-inside-work-tree 2>/dev/null) == 'true' ]] \
        && [[ $("git" ls-files --other --directory --exclude-standard | \
        sed q | wc -l | tr -d ' ') == 1 ]] ; then
            hook_com[unstaged]+="%{$fg_bold[red]%}??%{$reset_color%}"
    fi
}
precmd () { vcs_info }
RPROMPT='${vcs_info_msg_0_}'
###############################################################################



###############################################################################
# Keybindings (vi mode)
###############################################################################
bindkey '^P'    up-history
bindkey '^N'    down-history
bindkey '^?'    backward-delete-char
bindkey '^h'    backward-delete-char
bindkey '^w'    backward-kill-word
bindkey '^r'    history-incremental-search-backward
bindkey '^[[3~' delete-char
bindkey '^[[H'  beginning-of-line
bindkey '^[[1~' beginning-of-line
bindkey '^[[F'  end-of-line
bindkey '^[[4~' end-of-line
###############################################################################



###############################################################################
# Aliases
###############################################################################
alias cd..='cd ..'
alias ll='ls -l'
alias la='ls -A'
alias l='ls -CF'
alias sq='squeue -l -u $USER -o "%.18i %.17j %.10P %.8u %.8a %.8T %.8M \
%.12l %.6D  %R"'
if command -v 'checkssh' &>/dev/null; then
    alias git='checkssh; git'
    alias rsync='checkssh; rsync -aP'
    alias ssh='checkssh 1; ssh'
else
    alias rsync='rsync -aP'
fi
###############################################################################



###############################################################################
# Export Anaconda PATH (due to bug not possible in .zprofile or .zshenv)
###############################################################################
ANACONDA='anaconda3/bin'
[[ ":$PATH:" =~ ":$HOME/$ANACONDA:" ]] || export PATH="$HOME/$ANACONDA:$PATH"
###############################################################################
